apply plugin: 'com.android.application'

android {
    namespace 'org.orx.demo'
    compileSdk sdkVersion.toInteger()
    
    buildFeatures.prefab true

    defaultConfig {
        applicationId 'org.orx.demo'
        targetSdkVersion sdkVersion.toInteger()
        versionCode 1
        versionName '1.0'

        minSdkVersion 19
        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [ gameArguments: '' ]
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_11
        sourceCompatibility JavaVersion.VERSION_11
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
    sourceSets {
        main {
            // For custom apps, you should have resources in some parent folder with orx data.
            // E.g. '../../../data'
            assets.srcDirs = ['./src/main/assets']
        }
    }
    buildTypes {
        release {
            debuggable false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            externalNativeBuild.ndkBuild {
                arguments 'NDK_DEBUG=0'
            }
            manifestPlaceholders = [ gameArguments: 'orxDemo' ]
        }
        debug {
            debuggable true

            externalNativeBuild.ndkBuild {
                arguments 'NDK_DEBUG=1'
                cFlags '-D__orxDEBUG__'
            }
            manifestPlaceholders = [ gameArguments: 'orxDemoDebug' ]
        }
        profile {
            debuggable true

            externalNativeBuild.ndkBuild {
                arguments 'NDK_DEBUG=0'
                cFlags '-D__orxPROFILER__'
            }
            manifestPlaceholders = [ gameArguments: 'orxDemoProfile' ]
        }
    }
    lint {
        abortOnError true
    }
}
dependencies {
    releaseImplementation 'org.orx-project:orx:1.13'
    debugImplementation 'org.orx-project:orxd:1.13'
    profileImplementation 'org.orx-project:orxp:1.13'
}
