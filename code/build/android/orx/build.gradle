plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'org.orx.lib'
    compileSdk sdkVersion.toInteger()

    buildFeatures {
        prefab true
        prefabPublishing true
    }

    defaultConfig {
        minSdk 19
        targetSdk sdkVersion.toInteger()
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    prefab {
        orx {
            headers "${System.env.ORX}/include"
        }
        webpdecoder_static {
            headers "${System.env.ORX}/../extern/libwebp/src"
        }
        liquidfun_static {
            headers "${System.env.ORX}/../extern/LiquidFun-1.1.0/include"
        }
    }
    packagingOptions {
        exclude('**/libc++_shared.so')
    }
    buildTypes {
        release {

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            externalNativeBuild.ndkBuild {
                arguments 'NDK_DEBUG=0', '-j'
            }
        }
        debug {

            externalNativeBuild.ndkBuild {
                arguments 'NDK_DEBUG=1', '-j'
                cFlags '-D__orxDEBUG__'
            }
        }
        profile {

            externalNativeBuild.ndkBuild {
                arguments 'NDK_DEBUG=0', '-j'
                cFlags '-D__orxPROFILER__'
            }
        }
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
        singleVariant('debug') {
            withSourcesJar()
        }
        singleVariant('profile') {
            withSourcesJar()
        }
    }
    lint {
        abortOnError true
    }
}
dependencies {
    // Align versions of all Kotlin components
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))

    api 'androidx.core:core:1.12.0'
    api 'androidx.appcompat:appcompat:1.6.1'

    api 'androidx.games:games-activity:2.1.0-alpha02'
    api 'androidx.games:games-controller:2.0.1'
    api 'androidx.games:games-frame-pacing:2.1.0-rc01'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'org.orx-project'
                artifactId 'orx'
                version android.defaultConfig.versionName
            }
            debug(MavenPublication) {
                from components.debug
                groupId 'org.orx-project'
                artifactId 'orxd'
                version android.defaultConfig.versionName
            }
            profile(MavenPublication) {
                from components.profile
                groupId 'org.orx-project'
                artifactId 'orxp'
                version android.defaultConfig.versionName
            }
        }
        repositories {
            maven {
                name = 'orx'
                url "${System.env.ORX}/lib/static/android/repository/"
            }
        }
    }
}

androidComponents {
    onVariants(selector().all()) { variant->
        afterEvaluate {
            tasks.named("prefab${variant.name.capitalize()}Package").configure { task ->
                // WORKAROUND: Ideally, we would want this to be auto-generated from our modules.
                // See this FR: https://issuetracker.google.com/issues/214034366
                task.doLast {
                    copy {
                        from ('./src/main/prefab')
                        into ("./build/intermediates/prefab_package/${variant.name}/prefab")
                        include '**/*.json'
                    }
                }
            }
        }
    }
}
